varnishtest "Clean utm query parameters test"

server s1 {
    rxreq
    txresp -status 200 

    rxreq
    txresp -status 200 

    rxreq
    txresp -status 200 

    rxreq
    txresp -status 200 
} -start

varnish v1 -vcl+backend {
    sub vcl_recv {
        if (req.url ~ "(\?|&)utm_[a-z]+=") {
            set req.url = regsuball(req.url, "utm_[a-z]+=[^&=]+&?", "");
            set req.url = regsub(req.url, "[?|&]+$", "");
        }
    }

    sub vcl_deliver {
        set resp.http.X-Cleaned-Url = req.url;
    }
} -start

client c1 {
    txreq -url "/?utm_source=X&utm_medium=X&utm_campaign=X&utm_term=X&utm_content=X"
    rxresp
    expect resp.http.X-Cleaned-Url == "/"

    txreq -url "/?utm_source=X&utm_medium=X&utm_campaign=X&utm_term=X&utm_content=X&param1=1"
    rxresp
    expect resp.http.X-Cleaned-Url == "/?param1=1"

    txreq -url "/?utm_source=X&utm_medium=X&utm_campaign=X&utm_term=X&utm_content=X&param1=1&param2=2"
    rxresp
    expect resp.http.X-Cleaned-Url == "/?param1=1&param2=2"

    txreq -url "/?param1=1&utm_source=X&utm_medium=X&param2=2&utm_campaign=X&utm_term=X"
    rxresp
    expect resp.http.X-Cleaned-Url == "/?param1=1&param2=2"
} -run
